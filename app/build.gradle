apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.charlie.practice"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("${rootProject.projectDir.getAbsolutePath()}/keystore/debug.keystore")
        }
        release {
            def signingPropFile = new File("${rootProject.projectDir.getAbsolutePath()}/signing.properties")
            if (signingPropFile.exists()) {
                def signingProperties = new Properties()
                signingProperties.load(new FileInputStream(signingPropFile))

                storeFile file("${rootProject.projectDir.getAbsolutePath()}/keystore/yzj.keystore")
                storePassword signingProperties["store.pass"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.pass"]
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix 'dev'
            applicationIdSuffix '.test'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.1'
    testCompile 'junit:junit:4.12'
}
